plugins {
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id "jacoco"
    id "base"
}

dependencies {
    implementation project(":converter")
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = "5.2.1"
}

allprojects {

    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'base'

    if (!project.name.equals("model")) {
        apply plugin: 'checkstyle'
        apply plugin: 'pmd'
        apply plugin: 'jacoco'
        checkstyle {
            configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
            showViolations = true
            toolVersion '8.13'
        }

        tasks.withType(Checkstyle) {
            reports {
                xml.enabled false
                html.enabled true
                html.stylesheet rootProject.resources.text.fromFile('config/checkstyle/checkstyle-simple.xsl')
            }
        }


        pmd {
            toolVersion = "6.7.0"
            ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
            ruleSets = []
            ignoreFailures = false
        }

        jacocoTestReport {
            reports {
                xml.enabled false
                csv.enabled false
                html.destination file("${buildDir}/reports/jacoco")
            }
        }

        jacoco {
            toolVersion = "0.8.3"
        }


        check.dependsOn jacocoTestCoverageVerification
        jacocoTestCoverageVerification.dependsOn(jacocoTestReport)

        jacocoTestCoverageVerification {
            violationRules {
                rule {
                    limit {
                        counter = 'METHOD'
                        value = 'COVEREDRATIO'
                        minimum = 0.95
                    }
                }

                rule {
                    limit {
                        counter = 'CLASS'
                        value = 'COVEREDRATIO'

                        minimum = 0.95
                    }
                }

            }
        }

        test {
            testLogging {
                events "started","passed", "skipped","failed"
            }
        }

    }


}

subprojects {

    dependencies {

        implementation 'com.fasterxml.jackson.core:jackson-core:2.9.8'
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'


        testCompile 'junit:junit:4.12'
        testCompile 'org.hamcrest:hamcrest:2.1'


    }
}









